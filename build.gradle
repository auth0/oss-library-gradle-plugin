plugins {
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'groovy'
    id "com.gradle.plugin-publish" version "0.12.0"
}

repositories {
    google()
    mavenCentral()
    gradlePluginPortal()
}

tasks.wrapper {
    distributionType 'all'
}

dependencies {
    implementation "me.champeau.gradle:japicmp-gradle-plugin:0.2.9"
    // needed because of https://github.com/melix/japicmp-gradle-plugin/issues/36
    implementation 'com.google.guava:guava:28.2-jre'
    testImplementation("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude module: 'groovy-all'
    }
    testImplementation "org.hamcrest:hamcrest-core:1.3"
    testImplementation "com.android.tools.build:gradle:3.6.1"
}

compileJava {
    sourceCompatibility '1.7'
    targetCompatibility '1.7'
}

compileGroovy {
    sourceCompatibility '1.7'
    targetCompatibility '1.7'
}

tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.getRuntime().availableProcessors()

    testLogging {
        events 'passed', 'failed', 'skipped'
        showStandardStreams = true
        exceptionFormat 'full'
    }
}

version = "0.17.0"
group = "com.auth0.gradle"

gradlePlugin {
    plugins {
        javaLibraryPlugin {
            id = 'com.auth0.gradle.oss-library.java'
            implementationClass = 'com.auth0.gradle.oss.LibraryPlugin'
        }
        androidLibraryPlugin {
            id = 'com.auth0.gradle.oss-library.android'
            implementationClass = 'com.auth0.gradle.oss.AndroidLibraryPlugin'
        }
    }
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
    website = 'https://github.com/auth0/oss-library-gradle-plugin'
    vcsUrl = 'https://github.com/auth0/oss-library-gradle-plugin'
    description = 'Gradle plugin to configure a Java/Android oss library'
    tags = ['auth0', 'oss', 'java', 'build', 'android', 'publish']

    plugins {
        javaLibraryPlugin {
            id = 'com.auth0.gradle.oss-library.java'
            displayName = 'Auth0 OSS library plugin for Java libs'
        }
        androidLibraryPlugin {
            id = 'com.auth0.gradle.oss-library.android'
            displayName = 'Auth0 OSS library plugin for Android Java libs'
        }
    }
}
