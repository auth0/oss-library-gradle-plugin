buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.10.1'
    }
}

tasks.wrapper {
    distributionType 'all'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'com.gradle.plugin-publish'

repositories {
    google()
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    testImplementation ("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude module: 'groovy-all'
    }
    testImplementation "org.hamcrest:hamcrest-core:1.3"
    testImplementation "com.android.tools.build:gradle:3.6.1"
}

compileJava {
    sourceCompatibility '1.7'
    targetCompatibility '1.7'
}

compileGroovy {
    sourceCompatibility '1.7'
    targetCompatibility '1.7'
}

tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.getRuntime().availableProcessors()

    testLogging {
        events 'passed', 'failed', 'skipped'
        showStandardStreams = true
        exceptionFormat 'full'
    }
}

version = "0.11.0"
group = "com.auth0.gradle"

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
    website = 'https://github.com/auth0/oss-library-gradle-plugin'
    vcsUrl = 'https://github.com/auth0/oss-library-gradle-plugin'
    description = 'Gradle plugin to configure a Java/Android oss library'
    tags = ['auth0', 'oss', 'java', 'build', 'android', 'publish']

    plugins {
        javaLibraryPlugin {
            id = 'com.auth0.gradle.oss-library.java'
            displayName = 'Auth0 OSS library plugin for Java libs'
        }
        androidLibraryPlugin {
            id = 'com.auth0.gradle.oss-library.android'
            displayName = 'Auth0 OSS library plugin for Android Java libs'
        }
    }
}
