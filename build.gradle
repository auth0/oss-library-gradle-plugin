plugins {
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'groovy'
    id "com.gradle.plugin-publish" version "1.2.0"
}

repositories {
    google()
    mavenCentral()
    gradlePluginPortal()
}

tasks.wrapper {
    distributionType 'all'
}

dependencies {
    implementation "me.champeau.gradle:japicmp-gradle-plugin:0.2.9"
    // needed because of https://github.com/melix/japicmp-gradle-plugin/issues/36
    implementation 'com.google.guava:guava:30.0-jre'
    testImplementation("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude module: 'groovy-all'
    }
    testImplementation "org.hamcrest:hamcrest-core:1.3"
    testImplementation "com.android.tools.build:gradle:3.6.1"
}

compileJava {
    sourceCompatibility '1.7'
    targetCompatibility '1.7'
}

compileGroovy {
    sourceCompatibility '1.7'
    targetCompatibility '1.7'
}

tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.getRuntime().availableProcessors()

    testLogging {
        events 'passed', 'failed', 'skipped'
        showStandardStreams = true
        exceptionFormat 'full'
    }
}

version = "0.18.0"
group = "com.auth0.gradle"

gradlePlugin {
    website.set("https://github.com/auth0/oss-library-gradle-plugin")
    vcsUrl.set("https://github.com/auth0/oss-library-gradle-plugin")
    plugins {
        javaLibraryPlugin {
            id = 'com.auth0.gradle.oss-library.java'
            implementationClass = 'com.auth0.gradle.oss.LibraryPlugin'
            description = 'Gradle plugin to configure a Java library'
            displayName = 'Auth0 OSS library plugin for Java libraries'
            tags.set(['auth0', 'oss', 'java', 'build', 'publish'])
        }
        androidLibraryPlugin {
            id = 'com.auth0.gradle.oss-library.android'
            implementationClass = 'com.auth0.gradle.oss.AndroidLibraryPlugin'
            description = 'Gradle plugin to configure an Android library'
            displayName = 'Auth0 OSS library plugin for Android libraries'
            tags.set(['auth0', 'oss', 'java', 'build', 'android', 'publish'])
        }
    }
}
